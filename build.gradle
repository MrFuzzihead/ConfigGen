apply plugin: 'java'
apply plugin: 'maven'

group 'org.squiddev'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.6

configurations {
	deployerJars
}

repositories {
	mavenCentral()

	maven {
		name = "forge"
		url = "http://files.minecraftforge.net/maven"
	}
}

dependencies {
	compile 'com.squareup:javapoet:1.1.0'
	compile 'net.minecraftforge:forge:1.7.10-10.13.2.1291:universal'

	testCompile group: 'junit', name: 'junit', version: '4.+'
	deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar

	archives sourcesJar
	archives javadocJar
}

uploadArchives {
	repositories {
		if (!project.hasProperty("ftpHost")) {
			mavenLocal()
		} else {
			mavenDeployer {
				configuration = configurations.deployerJars
				repository(url: ftpHost) {
					authentication(userName: ftpUser, password: ftpPass)
				}

				pom.project {
					name 'ConfigGen'
					packaging 'jar'
					description 'Generate config loaders based off classes'
					url 'https://github.com/SquidDev/ConfigGen'

					scm {
						url 'scm:git@github.com:SquidDev/ConfigGen.git'
						connection 'scm:git@github.com:SquidDev/ConfigGen.git'
						developerConnection 'scm:git@github.com:SquidDev/ConfigGen.git'
					}

					licenses {
						license {
							name 'MIT License'
							url 'http://opensource.org/licenses/mit-license.php'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'SquidDev'
							name 'SquidDev'
						}
					}
				}
			}
		}
	}
}
